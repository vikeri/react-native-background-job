buildscript {
  repositories {
    jcenter()
    maven {
      url 'https://maven.google.com/'
      name 'Google'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.1'
  }
}

apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_BUILD_TOOLS_VERSION = '28.0.3'
def DEFAULT_TARGET_SDK_VERSION = 28

android {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
  if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
    namespace "com.pilloxa.backgroundjob"
  }
  compileSdkVersion project.hasProperty('compileSdkVersion') ? project.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
  buildToolsVersion project.hasProperty('buildToolsVersion') ? project.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

  // Uncomment to develop with yarn link into node_modules
  //    compileOptions.incremental = false
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion project.hasProperty('targetSdkVersion') ? project.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
    versionCode 2
    versionName "2.0"
    ndk {
      abiFilters "armeabi-v7a", "x86"
    }
  }
  lintOptions {
    warning 'InvalidPackage'
  }
}

repositories {
  jcenter()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$rootDir/../node_modules/react-native/android"
  }
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}

dependencies {
  implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
  implementation 'com.facebook.react:react-native:+'
  implementation 'androidx.annotation:annotation:1.1.0'
}
  
